# Maintainer: ajs124

pkgname=evdi-git
pkgver=1.5.0.r11.ga73092d
pkgrel=1
pkgdesc="A LinuxÂ® kernel module that enables management of multiple screens. Git version."
arch=('i686' 'x86_64')
url="https://github.com/DisplayLink/evdi"
license=('GPL')
depends=(dkms)
makedepends=(git libdrm)
install=$pkgname.install
changelog=$pkgname.Changelog
source=(git+https://github.com/DisplayLink/evdi/)
md5sums=(SKIP)
conflicts=(evdi)
provides=(evdi=$pkgver)

pkgver() {
	cd ${pkgname/-git/}
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	# We only need to build the library in this step, dmks will build the module
	cd ${pkgname/-git/}/library
	# hotfix mentioned at https://github.com/DisplayLink/evdi/issues/117
	sed -i -e 's/#if KERNEL_VERSION(4, 16, 0) <= LINUX_VERSION_CODE/#if KERNEL_VERSION(4, 15, 0) <= LINUX_VERSION_CODE/' ../module/evdi_connector.c
	make
}

package() {
	# Predfine some target folders
	SRCDIR="$pkgdir/usr/src/${pkgname/-git/}-$pkgver"	# This one is needed for dkms
	LIBNAME=lib${pkgname/-git/}

	cd ${pkgname/-git/}

	install -D -m 755 library/$LIBNAME.so $pkgdir/usr/lib/$LIBNAME.so

	install -d $SRCDIR
	install -D -m 755 module/* $SRCDIR
}
