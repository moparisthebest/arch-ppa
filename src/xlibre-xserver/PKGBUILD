# Maintainer:  Vitalii Kuzhdin <vitaliikuzhdin@gmail.com>
# Maintainer:  artist for XLibre

pkgbase="xlibre-xserver"
pkgname=("${pkgbase}"{,-bootstrap,-common,-devel,-xephyr,-xnest,-xvfb})
pkgver=25.0.0.9
pkgrel=2
arch=('aarch64' 'x86_64')
url="https://github.com/x11libre/xserver"
license=('LicenseRef-Adobe-Display-PostScript' 'BSD-3-Clause' 'LicenseRef-DEC-3-Clause' 
         'HPND' 'LicenseRef-HPND-sell-MIT-disclaimer-xserver' 'HPND-sell-variant' 
         'ICU' 'ISC' 'MIT' 'MIT-open-group' 'NTP' 'SGI-B-2.0' 'SMLNJ' 'X11'
         'X11-distribute-modifications-variant')
makedepends=('libepoxy' 'libpciaccess' 'libunwind' 'libx11' 'libxaw' 'libxcvt'
             'libxfont2' 'libxi' 'libxkbfile' 'libxmu' 'libxrender' 'libxres'
             'libxshmfence>=1.1' 'libxtst' 'libxv' 'mesa' 'mesa-libgl'
             'meson>=0.58' 'pixman>=0.27.2' 'systemd>=209' 'xcb-util'
             'xcb-util-image' 'xcb-util-keysyms' 'xcb-util-renderutil'
             'xcb-util-wm' 'xorg-font-util' 'xorg-util-macros' 'xorg-xkbcomp'
             'xorgproto>=7.0.31' 'xtrans>=1.3.5')
groups=('xlibre')
options=('!emptydirs')
_pkgsrc="xserver-xlibre-xserver-${pkgver}"
source=("${_pkgsrc}.tar.gz::${url}/archive/refs/tags/xlibre-xserver-${pkgver}.tar.gz"
        "xvfb-run"{,.1}) # with updates from FC master
b2sums=('bfd0c185d2b0de83229bf06baadef4811397279f0c196ddc634244728ca96abd7e968669f50977cd6f4d14b58406d8bdba07dc0f7f3c2559491d300b826e4874'
        '58c48ed893be841d14d3a09c9e1092a6da7bcb7fb773e1bf634c50a12e51ea3ad4aeba3843164a2834ee9f8ea95a7bca8b3ce8196a5328ce782724c082cb416f'
        '0e3738e099ee2b958df3e5a5adbdfcbd1150ad64645fdae70d74b50123c3f3d43f9f95f5e4bac82bf5c72b3deb978655b8c3177d37de0bd0a2a6e0e343863511')

build() {
  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, modules fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS="${CFLAGS/-fno-plt}"
  export CXXFLAGS="${CXXFLAGS/-fno-plt}"
  export LDFLAGS="${LDFLAGS/-Wl,-z,now}"
  local meson_options=(
    "${_pkgsrc}"
    "${_pkgsrc}/build"
    -D ipv6=true
    -D xvfb=true
    -D xnest=true
    -D xcsecurity=true
    -D xorg=true
    -D xephyr=true
    -D glamor=true
    -D udev=true
    -D dtrace=false
    -D systemd_logind=true
    -D suid_wrapper=true
    -D xkb_dir='/usr/share/X11/xkb'
    -D xkb_output_dir='/var/lib/xkb'
    -D libunwind=true
  )

  cd "${srcdir}"
  arch-meson "${meson_options[@]}"
  meson compile -C "${_pkgsrc}/build"
}

package_xlibre-xserver() {
  pkgdesc="XLibre X server"
  depends=('dbus' 'glibc' 'libdrm' 'libepoxy' 'libgl' 'libpciaccess' 'libtirpc'
           'libunwind' 'libxau' 'libxcvt' 'libxdmcp' 'libxfont2'
           'libxshmfence>=1.1' 'nettle' 'pixman>=0.27.2' 'sh'
           'systemd-libs>=209' 'xlibre-xserver-common' 'xlibre-input-libinput'
           "${pkgbase}-common=${pkgver}-${pkgrel}") # FS#52949
  # see xlibre-xserver*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  # and /usr/lib/pkgconfig/xorg-server.pc in xlibre-xserver-devel pkg
  provides=('X-ABI-VIDEODRV_VERSION=28.0' 'X-ABI-XINPUT_VERSION=26.0' 'X-ABI-EXTENSION_VERSION=11.0' 'x-server' 'xorg-server' xlibre-server)
  conflicts=('xorg-server' 'nvidia-utils<=331.20' 'glamor-egl' 'xf86-video-modesetting' 'xlibre-server')
  replaces=('xlibre-xserver-bootstrap' 'glamor-egl' 'xf86-video-modesetting')
  options=('emptydirs')
  install="${pkgname}.install"

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  cd "${pkgdir}"
  install -vdm755 "etc/X11/xorg.conf.d"

  cd "usr"
  # devel
  rm -rf "include" "lib/pkgconfig" "share/aclocal"
  # common
  rm -f "lib/xorg/protocol.txt" "share/man/man1/Xserver.1"
  # xephyr, xnest, xvfb
  find . -type f,l \( -name '*Xephyr*' -o -name '*Xnest*' -o -name '*Xvfb*' \) -delete
}

package_xlibre-xserver-bootstrap() {
  pkgdesc="XLibre X server (bootstrap)"
  depends=('dbus' 'glibc' 'libdrm' 'libepoxy' 'libgl' 'libpciaccess' 'libtirpc'
           'libunwind' 'libxau' 'libxcvt' 'libxdmcp' 'libxfont2' 'libmd'
           'libxshmfence>=1.1' 'nettle' 'pixman>=0.27.2' 'sh'
           'systemd-libs>=209'
           "${pkgbase}-common=${pkgver}-${pkgrel}") # FS#52949
  # see xlibre-xserver*/hw/xfree86/common/xf86Module.h for ABI versions - we provide major numbers that drivers can depend on
  # and /usr/lib/pkgconfig/xorg-server.pc in xlibre-xserver-devel pkg
  provides=("${pkgbase}" 'X-ABI-VIDEODRV_VERSION=28.0' 'X-ABI-XINPUT_VERSION=26.0' 'X-ABI-EXTENSION_VERSION=11.0' 'x-server' 'xorg-server') # xlibre'-server'
  conflicts=({xlibre,xorg}'-server' 'nvidia-utils<=331.20' 'glamor-egl' 'xf86-video-modesetting')
  # replaces=('glamor-egl' 'xf86-video-modesetting')
  options=('emptydirs')
  install="${pkgname}.install"

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  cd "${pkgdir}"
  install -vdm755 "etc/X11/xorg.conf.d"

  cd "usr"
  # devel
  rm -rf "include" "lib/pkgconfig" "share/aclocal"
  # common
  rm -f "lib/xorg/protocol.txt" "share/man/man1/Xserver.1"
  # xephyr, xnest, xvfb
  find . -type f,l \( -name '*Xephyr*' -o -name '*Xnest*' -o -name '*Xvfb*' \) -delete
}

package_xlibre-xserver-common() {
  pkgdesc="XLibre server common files"
  arch=('any')
  depends=('xkeyboard-config' 'xorg-setxkbmap' 'xorg-xkbcomp')
  provides=('xorg-server-common' 'xlibre-server-common')
  conflicts=('xorg-server-common' 'xlibre-server-common')

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  find "${pkgdir}" -type f,l ! \( -name 'protocol.txt' -o -name 'Xserver.1' \) -delete

  cd "${_pkgsrc}"
  install -vDm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgbase}/COPYING"
  install -vDm644 "xkb/README.compiled" "${pkgdir}/var/lib/xkb/README.compiled"
}

package_xlibre-xserver-devel() {
  pkgdesc="Development files for the XLibre X server"
  arch=('any')
  depends=('libpciaccess' 'mesa' 'pixman>=0.27.2' 'xorgproto>=7.0.31'
           'xorg-util-macros') # not technically required but almost every Xorg pkg needs it to build
  provides=('xorg-server-devel' 'xlibre-server-devel')
  conflicts=('xorg-server-devel' 'xlibre-server-devel')

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  find "${pkgdir}" -type f,l ! \( -name '*.h' -o -name '*.pc' -o -name '*.m4' \) -delete
}

package_xlibre-xserver-xephyr() {
  pkgdesc="A nested XLibre server that runs as an X application"
  depends=('glibc' 'libepoxy' 'libgl' 'libtirpc' 'libunwind' 'libx11' 'libxau'
           'libxdmcp' 'libxfont2' 'libxshmfence' 'nettle' 'pixman>=0.27.2'
           'systemd-libs>=209' 'xcb-util' 'xcb-util-image' 'xcb-util-keysyms'
           'xcb-util-renderutil' 'xcb-util-wm'
           "${pkgbase}-common=${pkgver}-${pkgrel}")
  provides=('xorg-server-xephyr' 'xlibre-server-xephyr')
  conflicts=('xorg-server-xephyr' 'xlibre-server-xephyr')

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  find "${pkgdir}" -type f,l ! -name '*Xephyr*' -delete
}

package_xlibre-xserver-xnest() {
  pkgdesc="A nested XLibre server that runs as an X application"
  depends=('glibc' 'libtirpc' 'libunwind' 'libx11' 'libxau' 'libxdmcp'
           'libxext' 'libxfont2' 'nettle' 'pixman>=0.27.2'
           'systemd-libs>=209' "${pkgbase}-common=${pkgver}-${pkgrel}")
  provides=('xorg-server-xnest' 'xlibre-server-xnest')
  conflicts=('xorg-server-xnest' 'xlibre-server-xnest')

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  find "${pkgdir}" -type f,l ! -name '*Xnest*' -delete
}

package_xlibre-xserver-xvfb() {
  pkgdesc="Virtual framebuffer XLibre server"
  license=('MIT' 'GPL-2.0-only')
  depends=('glibc' 'libgl' 'libtirpc' 'libunwind' 'libxau' 'libxdmcp'
           'libxfont2' 'nettle' 'pixman' 'sh' 'systemd-libs>=209'
           "${pkgbase}-common=${pkgver}-${pkgrel}" 'xorg-xauth')
  provides=('xorg-server-xvfb' 'xlibre-server-xvfb')
  conflicts=('xorg-server-xvfb' 'xlibre-server-xvfb')

  cd "${srcdir}"
  meson install -C "${_pkgsrc}/build" --destdir "${pkgdir}"

  find "${pkgdir}" -type f,l ! -name '*Xvfb*' -delete

  install -vDm755 "xvfb-run"   "${pkgdir}/usr/bin/xvfb-run"
  install -vDm644 "xvfb-run.1" "${pkgdir}/usr/share/man/man1/xvfb-run.1" # outda
}

