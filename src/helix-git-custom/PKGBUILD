# Maintainer: Robin Jadoul (aur@ur4ndom.dev)
# Contributor: Wojciech KÄ™pka (wojciech@wkepka.dev)
pkgname=helix-git-custom
_pkgname=helix
pkgver=24.03.r1210.gbabac28b
pkgrel=1
pkgdesc="A text editor written in rust"
url="https://helix-editor.com"
license=("MPL-2.0")
_git="https://github.com/moparisthebest/${_pkgname}.git"
arch=(x86_64)
makedepends=('git' 'cargo')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme')
provides=('hx')
conflicts=('helix')
options=(!lto)
source=("${_pkgname}::git+${_git}")
sha256sums=('SKIP')

_bin="hx"
_lib_path="/usr/lib/${_pkgname}"
_rt_path="${_lib_path}/runtime"


pkgver() {
    cd "${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_pkgname}"
    export CARGO_TARGET_DIR=target
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
    # cargo build --locked --profile opt
}

build() {
    cd "${_pkgname}"
    export CARGO_TARGET_DIR=target
    export HELIX_DISABLE_AUTO_GRAMMAR_BUILD=1
    cargo build --locked --profile opt
}

check() {
    cd "${_pkgname}"
    export CARGO_TARGET_DIR=target
    # cargo test --workspace --locked
    export RUSTFLAGS="${RUSTFLAGS} --cfg tokio_unstable"
    # cargo integration-test --locked
}

package() {
    cd "${_pkgname}"
    mkdir -p "${pkgdir}${_lib_path}"

    export HELIX_DEFAULT_RUNTIME=/usr/lib/helix/runtime
    ./target/opt/hx --grammar fetch
    ./target/opt/hx --grammar build
    
    
    rm -r  "runtime/grammars/sources"
    cp -r "runtime" "${pkgdir}${_lib_path}"
    install -Dm 0755 "target/opt/${_bin}" "${pkgdir}${_lib_path}/${_bin}"
    install -Dm 0644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
    install -Dm 0777 "${srcdir}/${_bin}" "${pkgdir}/usr/bin/${_bin}"
    install -Dm 0644 "contrib/Helix.desktop" "${pkgdir}/usr/share/applications/Helix.desktop"
    install -Dm 0644 "contrib/Helix.appdata.xml" "${pkgdir}/usr/share/appdata/Helix.appdata.xml"
    install -Dm 0644 "contrib/helix.png" "${pkgdir}/usr/share/icons/helix.png"
    install -Dm 0644 "contrib/completion/hx.zsh" "${pkgdir}/usr/share/zsh/site-functions/_hx"
    install -Dm 0644 "contrib/completion/hx.bash" "${pkgdir}/usr/share/bash-completion/completions/hx.bash"
    install -Dm 0644 "contrib/completion/hx.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/hx.fish"
}
