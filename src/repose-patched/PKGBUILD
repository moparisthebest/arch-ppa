# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=repose-patched
pkgver=7.1
pkgrel=3

provides=('repose')
conflicts=('repose')

pkgdesc="Arch Linux repo building tool"
url="https://github.com/vodik/repose"
arch=('x86_64')
license=('GPL-2.0-or-later')
depends=(
  'gpgme' 'libgpgme.so'
  'libarchive' 'libarchive.so'
  'openssl' 'libcrypto.so'
  'pacman' 'libalpm.so'
)
makedepends=(
  'ragel'
)
checkdepends=(
  'python-cffi'
  'python-pytest'
  'python-pytest-xdist'
  'python-setuptools'
)

source=("repose-$pkgver.tar.gz::https://github.com/vodik/repose/archive/$pkgver.tar.gz" "fix.patch")

sha256sums=('c23e93aca416e08e80b4d17a98fd593e6345d7da8806bdd3c5484977ac2c800d' 'd5c31768cca4e13ae764993cf2d42358645d40f4e2ea30c6e5a4807f09de3ab4')

build() {
  cd repose-$pkgver
  patch -p1 < ../fix.patch
  cd ..
  make -C repose-$pkgver
}

check() {
  cd repose-$pkgver
  pytest
}

package() {
  make -C repose-$pkgver DESTDIR="$pkgdir" install
}

# vim: ft=PKGBUILD et

